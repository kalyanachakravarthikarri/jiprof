buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3'
    }
}

description = 'JIP is a code profiling tool much like the hprof tool that ships with the JDK'

subprojects {
    apply plugin: 'java'
    apply plugin: 'findbugs'
    apply plugin: 'pmd'
    apply plugin: 'jacoco'
    apply plugin: 'com.github.kt3k.coveralls'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    version = '1.2'

    repositories {
        mavenCentral()
    }

    test.onlyIf {
        !Boolean.getBoolean('skip.tests')
    }

    findbugs {
        ignoreFailures = true
        effort = 'max'
    }

    pmd {
        ignoreFailures = true
        ruleSets = [
                'java-basic',
                'java-braces',
                'java-clone',
                'java-codesize',
                'java-comments',
                'java-controversial',
                'java-design',
                'java-empty',
                'java-finalizers',
                'java-imports',
                'java-j2ee',
                'java-javabeans',
                'java-junit',
                'java-logging-jakarta-commons',
                'java-logging-java',
                'java-migrating',
                'java-naming',
                'java-optimizations',
                'java-strictexception',
                'java-strings',
                'java-sunsecure',
                'java-typeresolution',
                'java-unnecessary',
                'java-unusedcode'
        ]
    }

    tasks.withType(Pmd) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    tasks.coveralls {
        dependsOn 'check'
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = false
        }
    }
}

project(':jip') {
    dependencies {
        compile 'org.ow2.asm:asm:5.0.4'

        compile fileTree(dir: 'lib', include: '*.jar')

        testCompile 'junit:junit:4.12'
    }
}

project(':jip-plugin') {
    jar {
        manifest {
            attributes 'Bundle-ManifestVersion': 2,
                    'Bundle-Name': 'JIP Plug-in',
                    'Bundle-SymbolicName': 'com.mentorgen.tools.profile; singleton:=true',
                    'Bundle-Version': '1.0.0',
                    'Bundle-Localization': 'plugin',
                    'Require-Bundle': 'org.eclipse.core.runtime, org.eclipse.ui',
                    'Eclipse-AutoStart': 'true',
                    'Import-Package': 'org.eclipse.core.runtime, org.eclipse.ui, org.eclipse.swt, org.eclipse.jface'
        }
    }
    dependencies {
        compile fileTree(dir: 'lib', include: '*.jar')
    }
}

project(':simple-profiler') {
    dependencies {
        compile 'org.ow2.asm:asm:5.0.4'
    }
}

project(':verbose-class') {
    jar {
        manifest {
            attributes 'Premain-Class': 'sample.verboseclass.Main'
        }
    }
}